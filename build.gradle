buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

project.ext.i2pVersion = '0.9.33'

String compat(String src) {
    if (src.contains('.')) {
        src.substring(src.lastIndexOf('.') + 1)
    } else {
        src
    }
}

configure([project(':core'), project(':webapp')]) {
    project.version = '0.4.7'

    project.afterEvaluate {
        jar {
            manifest {
                attributes 'Implementation-Version': project.version
            }
        }

        sourceCompatibility = 1.7
        targetCompatibility = 1.7

        def i2pBootClasspath
        // Set java7BootClasspath=/path/to/rt.jar:/path/to/jce.jar in ~/.gradle/gradle.properties if needed
        if (java7BootClasspath) {
            i2pBootClasspath = java7BootClasspath
        } else {
            def java7Home = System.getenv("JAVA7_HOME")
            if (java7Home) {
                i2pBootClasspath = "${java7Home}/jre/lib/jce.jar:${java7Home}/jre/lib/rt.jar"
            }
        }

        if (i2pBootClasspath) {
            project.tasks.withType(AbstractCompile, { AbstractCompile ac ->
                ac.options.bootstrapClasspath = files(i2pBootClasspath)
            })
        } else {
            if (JavaVersion.current().java8Compatible && !JavaVersion.current().java9Compatible) {
                throw new GradleException("Set java7BootClasspath property or JAVA7_HOME environment variable to enable cross-compilation, or run Gradle with JDK 9+")
            }
            tasks.withType(JavaCompile) {
                def version = compat(sourceCompatibility)
                println "Configuring $name to use --release $version"
                options.compilerArgs.addAll(['--release', version])
            }
        }
    }
}
